; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!
#define MyAppName "wxMusik" ; define variable
#define MyAppVer GetFileVersion("Release Unicode\" + MyAppName + ".exe") ; define variable
#define MyAppExe MyAppName + ".exe" ;
[Setup]
AppName={#MyAppName}
#emit 'AppVerName=' + MyAppName + ' ' + MyAppVer
AppPublisherURL=http://musik.berlios.de
AppSupportURL=http://musik.berlios.de
AppUpdatesURL=http://musik.berlios.de
DefaultDirName={pf}\{#MyAppName}
DefaultGroupName={#MyAppName}
AllowNoIcons=true
LicenseFile=license.txt

#emit 'OutputBaseFilename=' + MyAppName + '_v' + MyAppVer+ '_Setup'
VersionInfoVersion={#MyAppVer}
VersionInfoDescription=A music player and library application
Compression=lzma/ultra
ShowLanguageDialog=yes
AppID={{9EB87F75-0968-42F8-AD3F-01C2B13A1FE2}}
AlwaysShowDirOnReadyPage=true
AlwaysShowGroupOnReadyPage=true
AppCopyright=© 2004, Gunnar Roth, Casey Langen, Simon Windmill
DisableStartupPrompt=false
DisableFinishedPage=true
FlatComponentsList=false
ShowTasksTreeLines=true
ChangesAssociations=false
PrivilegesRequired=poweruser
AppMutex={#MyAppName}
InternalCompressLevel=ultra
SolidCompression=true
[Tasks]
Name: desktopicon; Description: {cm:CreateDesktopIcon}; GroupDescription: {cm:AdditionalIcons}
Name: quicklaunchicon; Description: {cm:CreateQuickLaunchIcon}; GroupDescription: {cm:AdditionalIcons}; Flags: unchecked

[Files]
Source: _release\{#MyAppExe}; DestDir: {app}; Flags: promptifolder; MinVersion: 4.1.1998,0; OnlyBelowVersion: 0,4.0.1381
Source: changelog.txt; DestDir: {app}; Flags: ignoreversion
Source: Release Unicode\fmod.dll; DestDir: {app}; Flags: ignoreversion
Source: license.txt; DestDir: {app}; Flags: ignoreversion
Source: _release\MMShellHook.dll; DestDir: {app}; Flags: ignoreversion
Source: ..\..\WINDOWS\SYSTEM32\msvcp71.dll; DestDir: {app}; Flags: promptifolder onlyifdoesntexist
Source: ..\..\WINDOWS\SYSTEM32\msvcr71.dll; DestDir: {app}; Flags: promptifolder onlyifdoesntexist
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

Source: contrib\playlists\*.mpd; DestDir: {userappdata}\..\.Musik\playlists; Flags: overwritereadonly; Components: Dynamic_Playlist_Examples
Source: contrib\playlists\*.mpu; DestDir: {userappdata}\..\.Musik\playlists; Flags: overwritereadonly; Components: Radio_Channels
Source: contrib\playlists\*.mpu; DestDir: {app}\.Musik\playlists; Flags: overwritereadonly; Components: Radio_Channels_win9x
Source: contrib\playlists\*.mpd; DestDir: {app}\.Musik\playlists; Flags: overwritereadonly; Components: Dynamic_Playlist_Examples_win9x
Source: Release Unicode\{#MyAppExe}; DestDir: {app}; MinVersion: 0,4.0.1381sp6; Flags: promptifolder
Source: locale\de\*.mo; DestDir: {app}\locale\de; Flags: overwritereadonly; Languages: de
Source: data\*; DestDir: {app}\data; Flags: overwritereadonly
Source: locale\fr\*.mo; DestDir: {app}\locale\fr; Flags: overwritereadonly; Languages: fr
Source: locale\cs\*.mo; DestDir: {app}\locale\cs; Flags: overwritereadonly; Languages: cs
Source: locale\nb\*.mo; DestDir: {app}\locale\nb; Flags: overwritereadonly; Languages: nb
Source: locale\pt_BR\*.mo; DestDir: {app}\locale\pt_BR; Flags: overwritereadonly; Languages: de pt
Source: locale\es\*.mo; DestDir: {app}\locale\es; Flags: overwritereadonly; Languages: es
Source: locale\nl\*.mo; DestDir: {app}\locale\nl; Flags: overwritereadonly; Languages: nl
Source: locale\it\*.mo; DestDir: {app}\locale\it; Flags: overwritereadonly; Languages: it
[Icons]
Name: {group}\{#MyAppName}; Filename: {app}\{#MyAppExe}; WorkingDir: {app}; IconIndex: 0
Name: {group}\{cm:UninstallProgram,{#MyAppName}}; Filename: {uninstallexe}
Name: {userdesktop}\{#MyAppName}; Filename: {app}\{#MyAppExe}; Tasks: desktopicon; WorkingDir: {app}; IconIndex: 0
Name: {userappdata}\Microsoft\Internet Explorer\Quick Launch\{#MyAppName}; Filename: {app}\{#MyAppExe}; Tasks: quicklaunchicon; WorkingDir: {app}; IconIndex: 0

[Run]
Filename: {app}\{#MyAppExe}; Description: {cm:LaunchProgram,{#MyAppName}}; Flags: nowait postinstall skipifsilent
[Components]
Name: Dynamic_Playlist_Examples; Description: {cm:Examples_of_Dynamic_Playlists}; Types: custom full; MinVersion: 0,4.0.1381
Name: Radio_Channels; Description: {cm:Examples_of_Net_Radio_Channels}; Types: custom full; MinVersion: 0,4.0.1381
Name: Radio_Channels_win9x; Description: {cm:Examples_of_Net_Radio_Channels}; Types: custom full; MinVersion: 4.0.950,0; OnlyBelowVersion: 0,4.0.1381
Name: Dynamic_Playlist_Examples_win9x; Description: {cm:Examples_of_Dynamic_Playlists}; Types: custom full; MinVersion: 4.0.950,0; OnlyBelowVersion: 0,4.0.1381
[InstallDelete]
Name: {app}\Musik.exe; Type: files
[UninstallDelete]
Name: {userappdata}\..\.Musik; Type: filesandordirs; MinVersion: 0,4.0.1381
Name: {app}\.Musik; Type: filesandordirs; MinVersion: 4.0.950,0; OnlyBelowVersion: 0,4.0.1381
[Registry]

[Languages]
Name: en; MessagesFile: compiler:Default.isl
Name: de; MessagesFile: compiler:Languages\German.isl
Name: fr; MessagesFile: compiler:Languages\French.isl
Name: cs; MessagesFile: compiler:Languages\Czech.isl
Name: nb; MessagesFile: compiler:Languages\Norwegian.isl
Name: pt; MessagesFile: compiler:Languages\PortugueseStd.isl
Name: es; MessagesFile: compiler:Languages\Spanish.isl
Name: nl; MessagesFile: compiler:Languages\Dutch.isl
Name: it; MessagesFile: compiler:Languages\Italian-3-4.2.1.isl

[CustomMessages]
en.Examples_of_Dynamic_Playlists=Examples of Dynamic Playlists
en.Examples_of_Net_Radio_Channels=Examples of Net Radio Channels
de.Examples_of_Dynamic_Playlists=Beispiele für dynamische Liedlisten
de.Examples_of_Net_Radio_Channels=Beispiele für Netzradios
fr.Examples_of_Dynamic_Playlists=Exemples de sélections dynamiques
fr.Examples_of_Net_Radio_Channels=Exemples de webradios
cs.Examples_of_Dynamic_Playlists=Ukázky dynamických seznamu
cs.Examples_of_Net_Radio_Channels=Ukázky Sítových Kanálu Rádia
cs.Examples_of_Dynamic_Playlists=Examples of Dynamic Playlists
cs.Examples_of_Net_Radio_Channels=Examples of Net Radio Channels
nb.Examples_of_Dynamic_Playlists=Eksempler på Dynamiske Spillelister
nb.Examples_of_Net_Radio_Channels=Eksempler på Nettradio Kanaler
pt.Examples_of_Dynamic_Playlists=Exemplos de Seleções musicais dinâmicas
pt.Examples_of_Net_Radio_Channels=Exemplos de webrádios
es.Examples_of_Dynamic_Playlists=Ejemplos de Listas de reproducción Dinámicas
es.Examples_of_Net_Radio_Channels=Ejemplos de canales de Radio de Red
nl.Examples_of_Dynamic_Playlists=Voorbeelden van dynamische afspeellijsten
nl.Examples_of_Net_Radio_Channels=Voorbeelden van webradiokanalen
it.Examples_of_Dynamic_Playlists=Esempi di Playlist Dinamiche
it.Examples_of_Net_Radio_Channels=Esempi di Canali Radio in Rete
